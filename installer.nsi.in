# NSIS installer script for for gnusim8085
# Copyright (C) 2008  Onkar Shinde <onkarshinde@gmail.com>
# LICENSE: Same as specified in COPYING file
#
# NOTE: Tested with NSIS 2.43, 2.46, 2.50 and 3.03. 2.43 is giving problem when
# trying to set icon for installer. You may have to disable icon if you are trying to
# build installer.

# Use modern UI
!include MUI2.nsh
# Use logic and version functions
!include LogicLib.nsh
!include WordFunc.nsh

# Required for proper handling of start menu on Windows Vista
RequestExecutionLevel admin

# Better compression, hence smaller size for installer
SetCompressor /SOLID lzma

# Define application name and installer file name
Name GNUSim8085
!ifndef MINGW_HOME
  !define MINGW_HOME "/usr/i686-w64-mingw32/sys-root/mingw"
!endif
!ifndef BUILD_DIR_TEMP
  !define BUILD_DIR_TEMP "win32"
!endif
OutFile @PACKAGE@-@VERSION@-installer.exe
!define GTK_VERSION_REQ "@GTK_VERSION_REQ@"
!define GTKSOURCEVIEW_API @GTKSOURCEVIEW_API@
!define MUI_ICON ${BUILD_DIR_TEMP}/share/pixmaps/@PACKAGE@/@PACKAGE@.ico
InstallDir "$PROGRAMFILES\GNUSim8085"
!define startmenu "$SMPROGRAMS\GNUSim8085"
!define uninstaller "uninstaller.exe"

# Show license page
!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE ${BUILD_DIR_TEMP}/share/doc/@PACKAGE@/COPYING

!insertmacro MUI_PAGE_COMPONENTS

# Show installation directory selector page
!insertmacro MUI_PAGE_DIRECTORY

# Show installation progress page
!insertmacro MUI_PAGE_INSTFILES

# Show finish page with an option to launch program
!define MUI_FINISHPAGE_RUN $INSTDIR\\@PACKAGE@.bat
!insertmacro MUI_PAGE_FINISH

# Show uninstall confirmation page
!insertmacro MUI_UNPAGE_CONFIRM

# Show uninstall progress page
!insertmacro MUI_UNPAGE_INSTFILES

# Set available UI languages for installer
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "Arabic"
!insertmacro MUI_LANGUAGE "Dutch"
!insertmacro MUI_LANGUAGE "Esperanto"
!insertmacro MUI_LANGUAGE "French"
!insertmacro MUI_LANGUAGE "German"
!insertmacro MUI_LANGUAGE "Greek"
!insertmacro MUI_LANGUAGE "Italian"
!insertmacro MUI_LANGUAGE "Japanese"
!insertmacro MUI_LANGUAGE "Lithuanian"
!insertmacro MUI_LANGUAGE "PortugueseBR"
!insertmacro MUI_LANGUAGE "Russian"
!insertmacro MUI_LANGUAGE "Spanish"
!insertmacro MUI_LANGUAGE "Thai"

# Show language selection dialog in installer
Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

# Install section
Section "!GNUSim8085" SecMain
SectionIn RO
  SetOutPath $INSTDIR
  File ${BUILD_DIR_TEMP}/share/doc/@PACKAGE@/*
  File /r ${BUILD_DIR_TEMP}/share/doc/@PACKAGE@/examples
  File /nonfatal /r ${BUILD_DIR_TEMP}/share/doc/@PACKAGE@/help

  SetOutPath $INSTDIR\pixmaps
  File ${BUILD_DIR_TEMP}/share/pixmaps/@PACKAGE@/*

  SetOutPath "$INSTDIR\data"
  File "${BUILD_DIR_TEMP}/share/@PACKAGE@/8085asm.lang"
  File "${MINGW_HOME}/share/${GTKSOURCEVIEW_API}/language-specs/language2.rng"
  File "${MINGW_HOME}/share/${GTKSOURCEVIEW_API}/language-specs/def.lang"
  File "${MINGW_HOME}/share/${GTKSOURCEVIEW_API}/styles/styles.rng"
  File "${MINGW_HOME}/share/${GTKSOURCEVIEW_API}/styles/classic.xml"

  SetOutPath "$INSTDIR\bin"
  File "${MINGW_HOME}/bin/*freetype*.dll"
  File "${MINGW_HOME}/bin/*intl*.dll"
  File "${MINGW_HOME}/bin/lib*win*.dll"
  File "${MINGW_HOME}/bin/libatk*.dll"
  File "${MINGW_HOME}/bin/libbz2*.dll"
  File "${MINGW_HOME}/bin/libcairo*.dll"
  File "${MINGW_HOME}/bin/libepoxy*.dll"
  File "${MINGW_HOME}/bin/libexpat*.dll"
  File "${MINGW_HOME}/bin/libffi*.dll"
  File "${MINGW_HOME}/bin/libfontconfig*.dll"
  File "${MINGW_HOME}/bin/libgcc*.dll"
  File "${MINGW_HOME}/bin/libgdk*.dll"
  File "${MINGW_HOME}/bin/libgio*.dll"
  File "${MINGW_HOME}/bin/libglib*.dll"
  File "${MINGW_HOME}/bin/libgmodule*.dll"
  File "${MINGW_HOME}/bin/libgobject*.dll"
  File "${MINGW_HOME}/bin/libgtk*.dll"
  File "${MINGW_HOME}/bin/libgthread*.dll"
  File "${MINGW_HOME}/bin/libpango*.dll"
  File "${MINGW_HOME}/bin/libpcre*.dll"
  File "${MINGW_HOME}/bin/libpixman*.dll"
  File "${MINGW_HOME}/bin/libpng*.dll"
  File "${MINGW_HOME}/bin/zlib1*.dll"
  File "${MINGW_HOME}/bin/iconv.dll"
  File "${MINGW_HOME}/bin/lib${GTKSOURCEVIEW_API}*.dll"
  File "${MINGW_HOME}/bin/libxml2*.dll"
  File "${MINGW_HOME}/bin/glib-compile-schemas.exe"
  File "${MINGW_HOME}/bin/gtk-update-icon-cache.exe"
  File ${BUILD_DIR_TEMP}/bin/*

  SetOutPath "$INSTDIR"
  FileOpen $0 @PACKAGE@.bat w
  FileWrite $0 'C:\Windows\System32\cmd.exe /c "SET XDG_DATA_DIRS=%XDG_DATA_DIRS%;share; && START /D ^"$INSTDIR^" bin\\@PACKAGE@.exe"'
  FileClose $0

  # Make example program files read only
  SetFileAttributes $INSTDIR\examples\addwithcarry.asm READONLY
  SetFileAttributes $INSTDIR\examples\bcd2bin.asm READONLY
  SetFileAttributes $INSTDIR\examples\debugexample.asm READONLY
  SetFileAttributes $INSTDIR\examples\nqueens.asm READONLY
  SetFileAttributes $INSTDIR\examples\sorting.asm READONLY
  SetFileAttributes $INSTDIR\examples\sumofnumbers.asm READONLY
  SetFileAttributes $INSTDIR\examples\swaphex.asm READONLY
  CreateDirectory ${startmenu}
  CreateShortcut "${startmenu}\GNUSim8085.lnk" "$INSTDIR\\@PACKAGE@.bat" "" "$INSTDIR\pixmaps\\@PACKAGE@.ico"
  WriteUninstaller $INSTDIR\${uninstaller}
  CreateShortcut "${startmenu}\Uninstall.lnk" "$INSTDIR\${uninstaller}"

  SetOutPath "$INSTDIR\share\icons"
  File /r "${MINGW_HOME}/share/icons/Adwaita"

  SetOutPath "$INSTDIR\share\glib-2.0\schemas"
  File "${MINGW_HOME}/share/glib-2.0/schemas/org.gtk.Settings.FileChooser.gschema.xml"

  SetOutPath "$INSTDIR\share\licenses"
  File /r "/usr/share/licenses/mingw32-adwaita-icon-theme"
  File /r "/usr/share/licenses/mingw32-atk"
  File /r "/usr/share/licenses/mingw32-cairo"
  File /r "/usr/share/licenses/mingw32-expat"
  File /r "/usr/share/licenses/mingw32-fontconfig"
  File /r "/usr/share/licenses/mingw32-freetype"
  File /r "/usr/share/licenses/mingw32-gdk-pixbuf"
  File /r "/usr/share/licenses/mingw32-glib2"
  File /r "/usr/share/licenses/mingw32-gtk3"
  File /r "/usr/share/licenses/mingw32-gtksourceview3"
  File /r "/usr/share/licenses/mingw32-gtk-update-icon-cache"
  File /r "/usr/share/licenses/mingw32-libepoxy"
  File /r "/usr/share/licenses/mingw32-libffi"
  File /r "/usr/share/licenses/mingw32-libpng"
  File /r "/usr/share/licenses/mingw32-pango"
  File /r "/usr/share/licenses/mingw32-pixman"
  File /r "/usr/share/licenses/mingw32-winpthreads"
  File /r "/usr/share/doc/mingw32-bzip2"
  File /r "/usr/share/doc/mingw32-gettext"
  File /r "/usr/share/doc/mingw32-libxml2"
  File /r "/usr/share/doc/mingw32-pcre"
  File /r "/usr/share/doc/mingw32-win-iconv"

  ReadEnvStr $0 COMSPEC
  SetOutPath "$INSTDIR\bin"
  nsExec::ExecToLog '"$0" /C glib-compile-schemas.exe ..\share\glib-2.0\schemas'
  nsExec::ExecToLog '"$0" /C gtk-update-icon-cache.exe ..\share\icons\Adwaita'
SectionEnd

!macro IncludeLocale LOCALE
  SetOutPath "$INSTDIR\share\locale\${LOCALE}\LC_MESSAGES"
  File "${BUILD_DIR_TEMP}/share/locale/${LOCALE}/LC_MESSAGES/@PACKAGE@.mo"
  File /nonfatal "${MINGW_HOME}/share/locale/${LOCALE}/LC_MESSAGES/gtk30.mo"
!macroend

Section "Translations" SecI18N
  !insertmacro IncludeLocale "ar"
  !insertmacro IncludeLocale "ast"
  !insertmacro IncludeLocale "de"
  !insertmacro IncludeLocale "el"
  !insertmacro IncludeLocale "eo"
  !insertmacro IncludeLocale "es"
  !insertmacro IncludeLocale "fr"
  !insertmacro IncludeLocale "gu"
  !insertmacro IncludeLocale "hi"
  !insertmacro IncludeLocale "it"
  !insertmacro IncludeLocale "ja"
  !insertmacro IncludeLocale "kn"
  !insertmacro IncludeLocale "lt"
  !insertmacro IncludeLocale "mr"
  !insertmacro IncludeLocale "nb_NO"
  !insertmacro IncludeLocale "nl"
  !insertmacro IncludeLocale "pt_BR"
  !insertmacro IncludeLocale "ru"
  !insertmacro IncludeLocale "ta"
  !insertmacro IncludeLocale "th"
  !system 'rm -rf ${BUILD_DIR_TEMP}'
SectionEnd

# Uninstall section
Section "Uninstall"
  Delete $INSTDIR\uninstaller.exe
  RMDir /r ${startmenu}
  RMDir /r $INSTDIR
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SecMain} "GNUSim8085 - Intel 8085 microprocessor simulator"
  !insertmacro MUI_DESCRIPTION_TEXT ${SecI18N} "Additional translations for the application UI (some may be incomplete)"
!insertmacro MUI_FUNCTION_DESCRIPTION_END

